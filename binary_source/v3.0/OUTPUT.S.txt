* OUTPUT routine. Connected with PR#A$nnnn
* This routine is handles output via COUT. Output
* (including basic PRINT statements) passes through
* here. Routes to screen and/or modem.
* Calls: MOUT, C3COUT1, DCDLOST.

OUTPUT   STA   :OUTTEMP
         PHA
         PHX
         PHY

         BIT   REMFLG     is modem active?
         BPL   :OUTEX     no, dump to screen only
         LDY   DCDSTATE   carrier present?
         BNE   :NOCARR    no

* Check for emulation

         LDY   PSEFLAG
         BNE   :MODEMCK   no filter

* Modem filter

         CMP   #$A0       non control?
         BGE   :MODEMCK

         CMP   #$8D       cr?
         BEQ   :MODEMCK
         CMP   #$88       backspace?
         BEQ   :MODEMCK
         CMP   #$87       ^G?
         BEQ   :MODEMCK
         CMP   #$8C       ^L?
         BEQ   :MODEMCK
         BNE   :EXIT      ;filter it!

:MODEMCK AND   #%01111111 kill high bit of output
         JSR   MOUT
         ORA   #%10000000 restore high bit

         CMP   #$8D       was it CR?
         BNE   :OUTEX     if not, we're done.

* BIT LFFLAG send LF flag on?
* BPL :OUTEX if not, we're done

         LDA   #$8A       add line feed after CR
         BRA   :MODEMCK   always

:NOCARR  JSR   DCDLOST    inform BASIC

:OUTEX   LDA   :OUTTEMP

         BIT   PRIVATE    private mode?
         BMI   :EXIT      if so, no screen output

         CMP   #$8D
         BEQ   :SCRPRT

         CMP   #$88
         BEQ   :SCRPRT

         CMP   #$8C       allow ^L to clear screen
         BEQ   :SCRPRT

         CMP   #$A0       control character?
         BLT   :EXIT      don't print it

:SCRPRT  JSR   C3COUT1    80 column printchar

:EXIT    PLY
         PLX
         PLA
         CLC
         RTS

:OUTTEMP DS    1
