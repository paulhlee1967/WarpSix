* FILTER: filters bad ctrl chars from input,
* handles case conversion. C=1 means the input
* was filtered. C=0 means input okay or modified.
* Calls: UPSHIFT, PRBFB. Destroys X,Y. Result in A.

FILTER   ORA   #%10000000 ensure high bit set
         CMP   #$8D       CR?
         BEQ   :CHAROK
         CMP   #$FF       delete?
         BEQ   :RUBOUT
         CMP   #$88       backspace?
         BEQ   :CHAROK
         CMP   #$98       ctrl-X?
         BEQ   :ERASELN
         CMP   #$A0       ctrl char still there?
         BLT   :NOCHAR2   yes, trash it

:CASECHK LDX   UCASEFLG
         BEQ   :CHAROK
         CPX   #255
         BEQ   :UP
:DOWN    JSR   DNSHIFT
         BRA   :CHAROK

:UP      JSR   UPSHIFT    convert to uppercase
:CHAROK  CLC              flag char as okay
         RTS              and return with char in A
:RUBOUT  LDA   #$88       convert to backspace
         BNE   :CHAROK    always
:NOCHAR2 SEC              flag char as bad
         RTS              and return with char in A

:ERASELN PHA              save char on stack
         LDA   MODEFLG
         CMP   #1         invisible mode?
         BNE   :GO        if not, erase
         STZ   NUMCHAR    nuke line, don't touch screen
         BRA   :NOERASE

:GO      LDA   NUMCHAR
         BEQ   :NOERASE   already at line start
:ERASE   JSR   PRBFB      Print <bs> <sp> <bs>
         DEC   NUMCHAR
         BNE   :ERASE     not at beginning of line yet
         JSR   CLRMBUF    make sure no junk appears
         JSR   SHOWCUR
:NOERASE PLA              restore A
         BRA   :NOCHAR2
