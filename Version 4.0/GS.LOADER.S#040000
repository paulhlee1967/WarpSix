**************************************************
*                                                *
*             Warp 6 BBS  GS.Loader              *
*                                                *
*                  Version 4.0                   *
*                                                *
**************************************************

* Copyright 2022 By Scott Johnson & Paul Lee

* Instructions: assemble this file to save the
* GS.LOADER to disk. You will need Merlin 16 or 16+
* to assemble this without getting an error.

* You can change the DSK pathname used to save the object
* code to whatever you desire.

----------------------------------
  DATE
----------------------------------

 LST OFF

 USE GSLOAD.MACS
 DSK GS.LOADER

BEGIN = $8C00 ;start of program
BEGIN0 = BEGIN-$300-$400 ;start of our buffers
 ;$300 for normal buffers
 ;$400 for file I/O buffer
 LST ON
SERGET3 = $950E
IRQMAST = $9522
QUICK = $9531
 LST OFF
OLDEXV = BEGIN+$04 ;to save old external vector
IRQV = BEGIN+$07 ;our IRQ handler address
PARSEV = BEGIN+$0B ;type cmd vector

SERSAV = SERGET3+5

* General equates

KEYBD = $C000
STROBE = $C010
MLI = $BF00
KVERSION = $BFFF
CROUT = $FD8E
GOSYSTEM = $BE70 ;MLI call
INBUF = $200 ;line input buf
ERROUT = $BE09 ;exit routine
BADCALL = $BE8B
SCLOSE = $BEDD ;close cmd parm area
EXTRNCMD = $BE06 ;JMP to address of cmd
DOSCMD = $BE03 ;basic.system mli vector
LEVEL = $BF94 ;file level
FREBUFR = $BEF8 ;free memory rtn
GETBFR = $BEF5 ;allocate memory rtn
MACHID = $BF98

VBLINT = $C019

* Environment routine equates

COUT = $FDED
MONZ = $FF69
RDKEY = $FD0C
PWRUP = $FAA6
HOME = $FC58 ;clear screen
TEXTON = $C051
COLD = $E000 ;AppleSoft coldstart
*DOS = $3D0
BI = $BE00
SETVID = $FE93

* Zero page use

PRTADR = $FA


 ORG $2000

ENVIRON CLD
 LDA #0
 STA UCFLAG
 LDA MLI ;check for ProDOS
 CMP #$4C
 BEQ :BAS
 JMP MNOPRO

:BAS LDA BI ;check for Basic.System
 CMP #$4C
 BEQ :OK
 JMP MNOBAS

* ID values returned by IDENTIFY

* 0 = Unknown or non-Apple
* 1 = Apple II
* 2 = Apple II Plus
* 3 = Apple III
* 4 = Original Apple IIe
* 5 = Enhanced Apple IIe
* 6 = Original Apple IIc
* 7 = Apple IIc w/ 3.5 ROM
* 8 = Apple IIc w/ Mem. Exp.
* 9 = Apple IIc w/ Rev. Mem. Exp.
* 10 = Apple IIc Plus
* 11 = Apple IIGS w/ ROM 00
* 12 = Apple IIGS w/ ROM 01
* 13 = Apple IIGS w/ ROM 03
* 14 = Newer Apple IIGS
* 15 = Newer than Apple IIGS
* 16 = Apple IIe Card in Mac LC

:OK JSR IDENTIFY
 STA SYSID
 CMP #0
 BNE :OLD
 JMP MUNKNOWN
:OLD CMP #3
 BGE :A3
 JMP MOLDCPU
:A3 CMP #3
 BNE :MORE
 JMP MA3
:MORE LDA MACHID
 AND #%00000010 ;80 columns?
 BNE :COL80
 JMP MOLDCPU
:COL80 JSR $C300 ;turn on 80 col. card
 LDA SYSID
 CMP #11
 BGE :CHECK1
 JMP MNEEDGS
:CHECK1 CMP #11 ;rom 00?
 BNE :CHECK2
 JMP MGSROM0
:CHECK2 CMP #16 ;IIe Card in Mac LC?
 BNE :CHECK3
 JMP MLCCARD
:CHECK3 CMP #14
 BGE :NEWCPU
 JMP LOADER ;system okay
:NEWCPU JMP MNEWCPU

SYSID DS 1

* Error message routines

MNOPRO
 LDA SYSID
 CMP #2+1
 BGE :LOWER
 LDA #$FF
 STA UCFLAG
:LOWER CLC ;clear screen and print
 JSR PRINT
 DA NOPRO
 JMP DOS ;exit to operating system

MNOBAS
 LDA SYSID
 CMP #2+1
 BGE :LOWER
 LDA #$FF
 STA UCFLAG
:LOWER CLC ;clear screen and print
 JSR PRINT
 DA NOBAS
 LDX #$FF
 TXS
 JSR DELAY1
 JMP COLD

MUNKNOWN
 LDA SYSID
 CMP #2+1
 BGE :LOWER
 LDA #$FF
 STA UCFLAG
:LOWER CLC ;clear screen and print
 JSR PRINT
 DA UNKNOWN
 LDX #$FF
 TXS
 JSR DELAY1
 JMP COLD

MA3
 CLC ;clear screen and print
 JSR PRINT
 DA A3
 LDX #$FF
 TXS
 JSR DELAY1
 JMP COLD

MOLDCPU
 LDA SYSID
 CMP #2+1
 BGE :LOWER
 LDA #$FF
 STA UCFLAG
:LOWER CLC ;clear screen and print
 JSR PRINT
 DA OLDCPU
 JMP DOS

MNEEDGS
 LDA SYSID
 CMP #2+1
 BGE :LOWER
 LDA #$FF
 STA UCFLAG
:LOWER CLC ;clear screen and print
 JSR PRINT
 DA NEEDGS
 JMP DOS

MGSROM0 CLC
 JSR PRINT
 DA GSROM0
 JSR DELAY1
 JMP LOADER

MLCCARD CLC
 JSR PRINT
 DA LCCARD
 JMP DOS

MNEWCPU CLC
 JSR PRINT
 DA NEWCPU
 JMP DOS

MFATAL
 LDA SYSID
 CMP #2+1
 BGE :LOWER
 LDA #$FF
 STA UCFLAG
:LOWER CLC ;clear screen and print
 JSR PRINT
 DA FATAL
 JMP DOS

MAPTALK
 CLC ;clear screen and print
 JSR PRINT
 DA APTALK
 JMP DOS

* Reserve space for Warp6.Driver & load into memory

LOADER LDA SYSID

* 11 = Apple IIGS w/ ROM 00
* 12 = Apple IIGS w/ ROM 01
* 13 = Apple IIGS w/ ROM 03

 XC ON
 CMP #13
 BGE :GONEW
 JSR OLDGS
 BCC :NEXT
 BRA MAPTALK

:GONEW JSR NEWGS
 BCC :NEXT
 BRA MAPTALK

:NEXT
 LDA KVERSION
 CMP #$04
 BLT :MCHK

 JSR ATLKCHK
 BCC MAPTALK

:MCHK JSR MODEM
 BCC RESERVE

 CLC ;clear screen first
 JSR PRINT ;print error
 DA YOURCARD
 JMP DOS

RESERVE CLD

 SEI
 JSR FREBUFR ;ensure memory available
 LDA #0
 STA LEVEL ;zero file level
 STA SCLOSE+1 ;indicate close for all files
 LDA #$CC ;do it
 CLD
 JSR GOSYSTEM ;execute close call using BI
 BCS ERROR

 LDA #$9A00-BEGIN0/$100 ;number of pages req'd

 JSR GETBFR
 BCS ERROR
 LDX #0
LOAD1 LDA CALL1,X ;load driver into memory
 BEQ LOAD2 ;with BLOAD cmd
 STA INBUF,X
 INX
 JMP LOAD1
LOAD2 JSR DOSCMD ;execute cmd through BI
 BCS ERROR
 LDA EXTRNCMD+1 ;save old external cmd vector
 STA OLDEXV+1
 LDA EXTRNCMD+2
 STA OLDEXV+2
 LDA #<PARSEV ;point to type cmd parser
 STA EXTRNCMD+1
 LDA #>PARSEV
 STA EXTRNCMD+2

 JSR GSSETUP

 JSR VBLTEST
 JSR ALLOCATE ;allocate our ProDOS IRQ handler
 JSR KILLFIRM ;don't let firmware handle SCC
 RTS ;return

ERROR JMP ERROUT
CALL1 ASC "BLOAD WARP6.DRIVER",8D00

ALLOCATE SEI

 LDA $3FE
 STA IRQV
 LDA $3FF
 STA IRQV+1

 XC ON
 XC ON
 CLC ;go to native mode
 XCE
 REP #$30 ;with 16-bit registers
 MX %00

 PEA $0000 ;push long
 PEA $0000
 PEA $0004 ;Interrupt Manager refnum

 LDX #$1103 ;GetVector
 JSL $E10000

 PLA ;save orgmgr address
 STA QUICK+1
 PLA
 STA QUICK+3

 PEA $0004 ;Interrupt Manager refnum
 PEA $0000 ;bank zero
 PEA #IRQMAST ;our manager address

 LDX #$1003 ;SetVector
 JSL $E10000

 SEC ;emulation mode
 XCE
 SEP #$30 ;with 8-bit registers
 MX %11

 XC OFF
 XC ON

 CLI
 RTS

PRINT BCS :BEGIN
 JSR CLEAR

 LDA PRTADR
 STA SAV1
 LDA PRTADR+1
 STA SAV2

:BEGIN PLA ;low byte (from the JSR)
 TAX
 INX
 TXA
 BNE :NOHI ;if not zero, don't inc high byte
:INCHI STA :RETURN+1 ;low byte of pointer is zero
 STA :GETPTR+1 ;insert low byte of pointer
 PLA ;get high byte
 TAX
 INX
 TXA
 STA :RETURN+2 ;save high byte of pointer
 STA :GETPTR+2 ;insert high byte of pointer
 JMP :NEXT
:NOHI STA :RETURN+1 ;save low byte of pointer
 STA :GETPTR+1 ;insert low byte of pointer
 PLA ;get high byte
 STA :RETURN+2 ;save high byte of pointer
 STA :GETPTR+2
:NEXT LDX #0
:GETPTR LDA $FFAA,X ;get pointer data
 STA PRTADR,X ;save pointer
 INX
 CPX #2
 BLT :GETPTR ;if X < 2, get next byte
:START LDX #$00
:PRLOOP LDA (PRTADR,X) ;Load and print a char.
 BEQ :PROUT ;Exit on zero byte.
 BIT UCFLAG
 BPL :DOIT
 JSR UPSHIFT
:DOIT JSR COUT
 INC PRTADR ;Increment address.
 BNE :PRLOOP
 INC PRTADR+1
 JMP :PRLOOP
:PROUT INC :RETURN+1
 BEQ :DOHI
:AFTER INC :RETURN+1
 BEQ :DOHI
:GOBACK SEC

 LDA SAV1
 STA PRTADR
 LDA SAV2
 STA PRTADR+1

:RETURN JMP $FFFF ;return to the program.
:DOHI INC :RETURN+2
 JMP :AFTER

SAV1 DS 1
SAV2 DS 1

CLEAR JSR HOME
 RTS

**************************
* IDENTIFIER Source Code *
* by Jim Ferr            *
* Adapted for Warp Six   *
**************************

* Identify the system and return result in A

IDENTIFY LDA $C081 ;read from ROM

 SEC ;see if new or old machine
 JSR $FE1F ;old machines have RTS here.
 BCS :OLD ;if carry still set, old.
 BCC :NEW ;if carry clear, new.

:OLD LDA $FBB3
 CMP #$38 ;old Apple II
 BEQ :A2
 CMP #$EA ;II Plus or Apple III
 BEQ :A2OR3
 CMP #$06
 BNE :BAD ;unknown

 LDA $FBC0
 BEQ :A2CFAM ;IIc family
 CMP #$EA
 BEQ :OLD2E ;unenhanced IIe
 CMP #$E0
 BNE :BAD ;unknown

* Determine if IIe enhanced or IIe Card in Mac LC

 LDA $FBDD
 CMP #$02
 BNE :NEW2E
 LDA $FBBE
 CMP #$00
 BNE :NEW2E
 BEQ :MACLC

:A2CFAM LDA $FBBF ;determine which IIc
 BEQ :A2C2 ;with 3.5 ROM
 CMP #$FF
 BEQ :A2C1 ;original IIc
 CMP #$03
 BEQ :A2C3 ;with org. mem. exp.
 CMP #$04
 BEQ :A2C4 ;with rev. mem. exp.
 CMP #$05
 BEQ :A2CPLUS ;IIc Plus
 BNE :BAD ;unknown

:A2OR3 LDA $FB1E
 CMP #$AD
 BEQ :A2PLUS ;II Plus
 CMP #$8A
 BNE :BAD ;unknown if not

 LDA #3 ;Apple III
 RTS

:BAD LDA #0 ;unknown machine
 RTS

:A2 LDA #1 ;original Apple II
 RTS

:A2PLUS LDA #2 ;Apple II Plus
 RTS

:OLD2E LDA #4 ;original IIe
 RTS

:NEW2E LDA #5 ;enhanced IIe
 RTS

:A2C1 LDA #6 ;original Apple IIc
 RTS
:A2C2 LDA #7 ;IIc w/ 3.5 ROM
 RTS
:A2C3 LDA #8 ;IIc w/ org. mem exp.
 RTS
:A2C4 LDA #9 ;IIc w/ rev. mem exp.
 RTS

:A2CPLUS LDA #10 ;IIc Plus
 RTS

:NEW CLC ;get into 16 bit mode
 DFB $FB ;XCE
 PHP ;save processor status
 DFB $C2
 DFB $30 ;REP 30, sets 16 bit
 JSR $FE1F ;call ID routine again
 STY :NEWY
 PLP ;restore 8 bit status
 DFB $FB ;XCE

 LDA :NEWY+1 ;get Machine ID number
 BNE :NEWER

 LDA :NEWY ;ROM version number
 BEQ :GSROM00 ;ROM 00
 CMP #$01
 BEQ :GSROM01 ;ROM 01
 CMP #$03
 BEQ :GSROM03 ;ROM 03
 BNE :NEWGS

:GSROM00 LDA #11
 RTS
:GSROM01 LDA #12
 RTS
:GSROM03 LDA #13
 RTS
:NEWGS LDA #14
 RTS
:NEWER LDA #15
 RTS
:MACLC LDA #16
 RTS

* Storage

:NEWY DW $0000

DELAY1 LDA DELAY1B delay a little over 1 second
 STA :DELAY
:LOOP LDY #$FF
 JSR DELAYMS
 DEC :DELAY
 BNE :LOOP
 RTS
:DELAY DFB 0

* DELAYMS: pause for Y=ms

DELAYMS CPY #0
 BEQ :DELAYOUT
 NOP
 CPY #1
 BNE :DA
 JMP :FINAL

:DA DEY

:D0 LDX #202

:D1 DEX
 BNE :D1
 NOP
 NOP
 DEY
 BNE :D0

:FINAL LDX #199

:D2 DEX
 BNE :D2

:DELAYOUT RTS

DELAY1B DFB 25


* KILLFIRM: disable firmware interrupt handler for
* our serial port

XCE = $FB
REP = $C2
SEP = $E2
PEA = $F4
JSL = $22
LDAL = $AF
STAL = $8F

KILLFIRM LDA SYSID
 CMP #11 ;ROM 00?
 BEQ SERGET1 ;skip the toolbox call on ROM 00

 CLC ;native mode
 DFB XCE
 DFB REP #%00110000 ;clear m & x flags
 DFB %00110000 ;clear m & x flags

 DFB PEA
 DW $0000 ;push space for result
 DFB PEA
 DW $0000

 DFB PEA
 DW $000E ;SerFlag reference no.

 DFB $A2 ;LDX
 DW $1603 ;GetAdr call
 DFB JSL
 DW $0000
 DFB $E1 ;JSL ToolLocator

 DFB SEP
 DFB %00110000 ;set m & x flags
 SEC ;back to emulation mode
 DFB XCE

 PLA ;get low byte
 STA SERGET1+1
 STA SERGET2+1
 STA SERGET3+1
 PLA ;get high byte
 STA SERGET1+2
 STA SERGET2+2
 STA SERGET3+2
 PLA ;get bank byte
 STA SERGET1+3
 STA SERGET2+3
 STA SERGET3+3
 PLA ;get extra byte off stack!

SERGET1 DFB LDAL
 DW $0104 ;get SerFlag
 DFB $E1

 STA SERSAV ;save value
 ORA #%00000111 ;disable firmware IRQ handler
 ;for serial port B (Modem)
 ;%00111000 for serial port A

SERGET2 DFB STAL ;save SerFlag
 DW $0104
 DFB $E1

* Use firmware to set up IIGS port for Warp Six

GSSETUP LDA $C20D ;set up jump table
 STA GSINIT+1 ;for Pascal 1.1 firmware
 LDA $C20E
 STA GSREAD+1
 LDA $C20F
 STA GSWRITE+1
 LDA $C210
 STA GSSTAT+1
 LDA $C212
 STA GSCTRL+1

 LDX #$C2 ;first, reset the port!
 LDY #$20 ;(absolutely necessary)
 JSR GSINIT
 CPX #0 ;any error?
 BEQ :NEXT
 JMP MFATAL

:NEXT LDA #<:CMDLIST ;next, call extended interface
 LDX #>:CMDLIST
 LDY #0
 JSR GSCTRL

 LDA :RESULT
 BEQ :CHECK
 JMP MFATAL

:CMDLIST DFB $03 ;GetModeBits call
 DFB $00
:RESULT DW $0000

:MODEBITS DW $0000 ;long word returned
 DW $0000

:CHECK LDA :MODEBITS ;determine if comm or ptr
 AND #%00000001 ;check b0 (1=comm 0=ptr)
 BEQ :CMDI

* Save command character

:CMDA LDA #$01 ;Control-A
 STA CMDCHAR
 BNE :CMD0
:CMDI LDA #$09 ;Control-I
 STA CMDCHAR

* Ensure buffering is off

:CMD0 LDA :MODEBITS+1
 AND #%01000000 ;if buffering disabled, go
 BEQ :READY ;to start of program

* Issue buffering disable command...

:CMD LDA CMDCHAR
 LDX #$C2
 LDY #$20
 JSR GSWRITE ;Buffering Disable cmd

:B LDA #'B'
 LDX #$C2
 LDY #$20
 JSR GSWRITE

 LDA #'D'
 LDX #$C2
 LDY #$20
 JSR GSWRITE
:READY RTS ;GS port setup complete!

CMDCHAR DS 1 ;IIGS serial port cmd char

* Pascal 1.1 Protocol table for IIGS

GSINIT JMP $C200
GSREAD JMP $C200
GSWRITE JMP $C200
GSSTAT JMP $C200
GSCTRL JMP $C200


ATLKCHK JSR MLI
 DFB $42 ;AppleTalk
 DW APPARMS
 RTS ;If carry set, AppleTalk off.

APPARMS DFB $00 ;anynchronous call
 DFB $02 ;GetInfo call number
ARESULT DS 13 ;result

MODEM

* Check if Slot 2 is set to int. ($00)
* If so, Slot 2 is set to Modem Port.

 JSR SLOT2
 BEQ :OK
 SEC
 RTS
:OK CLC
 RTS

 XC ON
 XC ON

OLDGS

* Check if Slot 1 is ext. ($01) and Slot 7
* is int. ($00) -- if so, AppleTalk is on.

 JSR SLOT1
 CMP #$01
 BNE :OK
 JSR SLOT7
 BNE :OK
 SEC
 RTS
:OK CLC
 RTS

NEWGS

* Check if either slot 1 or slot 2's type is $02
* If so, AppleTalk is on.

 JSR SLOT1
 CMP #$02
 BEQ :BAD
 JSR SLOT2
 CMP #$02
 BEQ :BAD
 CLC
 RTS
:BAD SEC
 RTS


SLOT1 CLC ;to native mode
 XCE
 REP #$30 ;16 bit m and x
 LONGA 1
 LONGI 1

 PEA $0000 ;space for result
 PEA $0021 ;Slot 1 setting
 _ReadBParam ;read battery RAM parameter
 ;(2 byte result left on stack)

 PLA ;get port 2 type (2 bytes)

 SEC ;emulation mode
 XCE
 LONGA 0
 LONGI 0
 RTS

 XC ON
 XC ON

SLOT7 CLC ;to native mode
 XCE
 REP #$30 ;16 bit m and x
 LONGA 1
 LONGI 1

 PEA $0000 ;space for result
 PEA $0027 ;Slot 7 setting
 _ReadBParam ;read battery RAM parameter
 ;(2 byte result left on stack)

 PLA ;get port 7 type (2 bytes)

 SEC ;emulation mode
 XCE
 LONGA 0
 LONGI 0
 RTS

SLOT2
 CLC ;to native mode
 XCE
 REP #$30 ;16 bit m and x
 LONGA 1
 LONGI 1

 PEA $0000 ;space for result
 PEA $0022 ;Slot 2 setting
 _ReadBParam ;read battery RAM parameter
 ;(2 byte result left on stack)

 PLA ;get port 2 type (2 bytes)

 SEC ;emulation mode
 XCE
 LONGA 0
 LONGI 0
 RTS

 XC OFF
 XC OFF

* Test if this machine has VBL. Sets NONVBL flag.

VBLTEST LDX #$FF
 LDY #$25
:TESTVBL LDA VBLINT
 BPL :NOCHANGE
 BMI :GOOD
:NOCHANGE DEX ;dec 1st counter
 BNE :TESTVBL ;loop if non-zero
 DEY ;dec 2nd counter
 BNE :TESTVBL ;loop if non-zero
:BAD
 LDA SYSID
 CMP #2+1
 BGE :LOWER
 LDA #$FF
 STA UCFLAG
:LOWER CLC ;clear screen and print
 JSR PRINT
 DA NOVBL
 JMP DOS

:GOOD RTS

 PUT ERRORS

DOS EQU *


 LDA SYSID
 CMP #2+1
 BGE :LOWER
 LDA #$FF
 STA UCFLAG
:LOWER SEC ;don't clear screen
 JSR PRINT
 DA EXIT

:KEYWAIT LDA KEYBD
 BPL :KEYWAIT
 STA STROBE
 ;and fall through.

QUIT JSR MLI
 DFB $65 ;Quit call
 DA :QPARMS
 BRK
 BRK

:QPARMS DFB 4
 DFB 0
 DW $0000
 DFB 0
 DW $0000

EXIT HEX 8D
 ASC "Press Return to exit : ",00

UPSHIFT CMP #"z"+1
 BGE :NO
 CMP #"a"
 BLT :NO
 AND #%11011111 convert to uppercase
:NO RTS

UCFLAG DFB 0
