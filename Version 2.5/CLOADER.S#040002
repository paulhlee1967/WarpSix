 LST OFF
 DSK /WARP6/V25/DRIVERS/CLOADER

* CLOADER (Copy command Loader)
* Public domain version 2.5.

* Instructions: assemble this file to save the CLOADER
* module to disk.

* You can change the DSK pathname used to save the object
* code to whatever you desire.

BHIMEM = $BEFB
FREBUFR = $BEF8 ;free memory rtn
GETBUFR = $BEF5
ERROUT = $BE09
MEMSIZ = $73 ;current HIMEM, low/high

BEGIN = $8D00 ;we need up to $99FF

COPY = BEGIN-$300-$400-$500

   ;COPY needs $500 bytes

PAGES = BEGIN-$300-$400-COPY/$100

EMPTY = $82 ;himem w/o COPY loaded
FULL = $7D ;himem with COPY loaded

INBUF = $200 ;line input buf
DOSCMD = $BE03 ;basic.system mli vector

 ORG $300

 JMP START

BHSAVE DFB 0
TEMP DFB 0
FLAG DFB 0

START LDA FLAG ;request flag
 BEQ RESERVE ;if zero, try to reserve
 JMP REMOVE ;if not, try to remove

* RESERVE: get space for COPY module and load it.

RESERVE
 LDA MEMSIZ+1
 CMP #EMPTY ;HIMEM without Xmodem loaded
 BEQ RES1 ;branch if correct
 LDA #11 ;invalid option
 JMP ERROUT


RES1 LDA BHIMEM ;get boot value of HIMEM
 STA BHSAVE ;save it
 LDA MEMSIZ+1 ;get current HIMEM
 STA TEMP ;save it

 LDA #PAGES ;allocate memory for COPY
 JSR GETBUFR
 BNE ERROR

 LDA TEMP ;get saved HIMEM before call
 STA BHIMEM ;pretend it is boot HIMEM

LOAD LDX #0
LOAD1 LDA CALL1,X ;load it into memory
 STA INBUF,X
 BEQ LOAD2
 INX
 JMP LOAD1
LOAD2 JSR DOSCMD ;execute cmd through BI
 BCS ERROR
 CLC
 RTS

ERROR JMP ERROUT
CALL1 ASC "BLOAD COPY",8D00

* REMOVE COPY from memory, freeing it for BASIC

REMOVE
 LDA MEMSIZ+1 ;get HIMEM w/o Xmodem
 CMP #FULL ;is our module here?
 BEQ REM1 ;if so, remove it
 CLC  ;if not, do nothing.
 RTS

REM1 JSR FREBUFR
 LDA BHSAVE ;get saved BHIMEM
 STA BHIMEM ;restore original value
 CLC  ;clear carry for BASIC
 RTS
